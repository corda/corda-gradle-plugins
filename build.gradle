buildscript {
    ext {
        gradle_plugins_version = '4.0.37-SNAPSHOT'
        bouncycastle_version = '1.57'
        typesafe_config_version = '1.3.1'
        jsr305_version = '3.0.2'
        kotlin_version = '1.2.71'
        artifactory_plugin_version = '4.7.3'
        snake_yaml_version = '1.19'
        commons_io_version = '2.6'
        assertj_version = '3.9.1'
        junit_version = '4.12'
        asm_version = '6.2.1'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:$artifactory_plugin_version"
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.10.0' apply false
}

apply plugin: 'com.jfrog.artifactory'

ext {
    vcsUrl = 'https://github.com/corda/corda-gradle-plugins'
}

allprojects {
    version gradle_plugins_version
    group 'net.corda.plugins'

    tasks.withType(JavaCompile).all {
        options.encoding = 'UTF-8'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // When writing Gradle plugins in Kotlin, we need to restrict
            // ourselves to the same Kotlin API that Gradle itself uses.
            // Gradle 4.10.x uses Kotlin 1.2.
            jvmTarget = "1.8"
            apiVersion = "1.2"
            languageVersion = "1.2"
            freeCompilerArgs = ['-Xjvm-default=enable']
        }
    }

    tasks.withType(Test) {
        // Prevent the project from creating temporary files outside of the build directory.
        systemProperty 'java.io.tmpdir', buildDir.absolutePath

        // Tell the tests where Gradle's current module cache is.
        // We need the tests to share this module cache to prevent the
        // Gradle Test-Kit from downloading its own copy of Kotlin etc.
        systemProperty 'test.gradle.user.home', project.gradle.gradleUserHomeDir
    }
}

// Only the top-level projects are plugins, so only publish these ones.
// The "child projects" are the immediate children of the root. Any
// "grandchild" project is considered to be internal to its parent.
def publishProjects = project.childProjects.values()

configure(publishProjects) { subproject ->
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'com.gradle.plugin-publish'

    task sourceJar(type: Jar, dependsOn: subproject.classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: subproject.javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task install(dependsOn: 'publishToMavenLocal')

    publishing {
        publications {
            create(subproject.name, MavenPublication) {
                if (subproject.hasProperty("mavenArtifacts")) {
                    subproject.mavenArtifacts.call(it)
                } else {
                    from components.java
                }

                groupId subproject.group
                artifactId subproject.name
                artifact tasks.sourceJar
                artifact tasks.javadocJar

                pom {
                    name = subproject.name
                    description = subproject.description
                    url = vcsUrl
                    scm {
                        url = vcsUrl
                    }

                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'R3'
                            name = 'R3'
                            email = 'dev@corda.net'
                        }
                    }
                }

                if (subproject.hasProperty("mavenPom")) {
                    subproject.mavenPom.call(pom)
                }
            }
        }
    }

    bintray {
        user = System.getenv('CORDA_BINTRAY_USER') ?: System.getProperty('corda.bintray.user')
        key = System.getenv('CORDA_BINTRAY_KEY') ?: System.getProperty('corda.bintray.key')
        publications = [subproject.name]
        dryRun = (System.getenv('CORDA_BINTRAY_DRYRUN') != null || System.getProperty('corda.bintray.dryrun') != null)
        pkg {
            repo = 'corda'
            name = subproject.name
            userOrg = 'r3'
            licenses = ['Apache-2.0']

            version {
                gpg {
                    sign = true
                    passphrase = System.getenv('CORDA_BINTRAY_GPG_PASSPHRASE')
                }
            }
        }
    }

    pluginBundle {
        mavenCoordinates {
            groupId = project.group
        }
    }
}

artifactory {
    publish {
        contextUrl = 'https://ci-artifactory.corda.r3cev.com/artifactory'
        repository {
            repoKey = 'corda-dev'
            username = System.getenv('CORDA_ARTIFACTORY_USERNAME') ?: System.getProperty('corda.artifactory.username')
            password = System.getenv('CORDA_ARTIFACTORY_PASSWORD') ?: System.getProperty('corda.artifactory.password')
        }

        defaults {
            if (publishProjects.contains(project)) {
                publications(project.name)
            }
        }
    }
}

wrapper {
    gradleVersion = "4.10.1"
    distributionType = Wrapper.DistributionType.ALL
}

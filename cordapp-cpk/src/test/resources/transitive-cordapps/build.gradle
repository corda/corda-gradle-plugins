import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import static org.gradle.api.JavaVersion.VERSION_1_8

plugins {
    id 'net.corda.plugins.cordapp-cpk'
    id 'org.jetbrains.kotlin.jvm'
}

apply from: 'repositories.gradle'

allprojects {
    group = 'com.example'

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = VERSION_1_8
        targetCompatibility = VERSION_1_8
    }
}

if (with_publishing.toBoolean()) {
    allprojects {
        apply plugin: 'java-library'
        apply plugin: 'maven-publish'

        publishing {
            publications {
                maven(MavenPublication) {
                    from components.java
                }
            }
        }
    }
}

version = cordapp_version

cordapp {
    targetPlatformVersion = platform_version.toInteger()
    minimumPlatformVersion = platform_version.toInteger()

    contract {
        name = 'Transitive CorDapps'
        versionId = cordapp_contract_version.toInteger()
        licence = 'Test-Licence'
        vendor = 'R3'
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = VERSION_1_8
        apiVersion = '1.3'
        languageVersion = '1.3'
        freeCompilerArgs = ['-Xjvm-default=enable']
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-osgi-bundle:$kotlin_version"
    implementation "commons-io:commons-io:$commons_io_version"
    cordapp project(':cpk-three')
}

plugins {
    id 'biz.aQute.bnd.builder'
    id 'java-library'
    id 'maven-publish'
}
apply from: '../javaTarget.gradle'

group = 'net.corda'
version = corda_api_version

// Share the source files, but not the output files.
buildDir = rootProject.file('cordaBuild')

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.osgi:osgi.annotation:$osgi_version"
    compileOnly "org.jetbrains:annotations:$jetbrains_annotations_version"
    api "javax.persistence:javax.persistence-api:$persistence_api_version"
    api "org.slf4j:slf4j-api:$corda_slf4j_version"
    runtimeOnly "com.google.guava:guava:$corda_guava_version"
}

tasks.named('jar', Jar) {
    manifest {
        attributes('Corda-Platform-Version': 1000)
    }

    bundle {
        bnd '''\
Bundle-SymbolicName: net.corda.api
Bundle-Name: Fake Corda APIs
'''
    }
}

publishing {
    publications {
        cordaApi(MavenPublication) {
            from components.java

            pom {
                name = 'Corda API'
            }
        }
    }
}

plugins {
    id 'net.corda.plugins.cordapp-cpk'
}

rootProject.allprojects {
    if (path == ':corda-platform') {
        pluginManager.withPlugin('java-platform') {
            // Include this CorDapp into the platform.
            dependencies {
                constraints {
                    api project(':corda-platform-cordapp')
                }
            }
        }
    }
}

group = 'net.corda'
version = platform_cordapp_version

cordapp {
    targetPlatformVersion = platform_version.toInteger()

    contract {
        name = 'Corda platform CorDapp'
        versionId = cordapp_contract_version.toInteger()
        licence = 'Test-Licence'
        vendor = 'R3'
    }
}

dependencies {
    // The CorDapp and the platform are mutual dependencies.
    cordaProvided platform(project(':corda-platform'))
    cordaProvided project(':corda-api')
}

tasks.named('jar', Jar) {
    manifest {
        attributes(['Corda-CPK-Type': 'corda-api'])
    }
}

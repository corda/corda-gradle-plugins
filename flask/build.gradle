plugins {
    id 'com.gradle.plugin-publish'
    id "java-gradle-plugin"
    id 'groovy'
}

description 'Packages an application as an executable jar of jars.'

evaluationDependsOn("flask-common")
evaluationDependsOn("flask-launcher")

dependencies {
    compileOnly project("flask-common")
}

Provider<Copy> copyLauncher = tasks.register("copyLauncher", Copy) {
    from(project("flask-launcher").tasks.named("tar").map {it.outputs })
    into(new File(project.buildDir, "resources/main/META-INF"))
}

Provider<Copy> copyHeartbeatAgent = tasks.register("copyHeartbeatAgent", Copy) {
    from(project("flask-heartbeat-agent").tasks.named("jar").map {it.outputs })
    into(new File(project.buildDir, "resources/main/META-INF"))
}

tasks.named("processResources") {
    inputs.files(copyLauncher)
    inputs.files(copyHeartbeatAgent)
}

jar {
    manifest {
        attributes "version" : archiveVersion.get()
    }
    from(project("flask-common").tasks.named("jar").map { zipTree(it.outputs.files.singleFile) })
}

gradlePlugin {
    plugins {
        flaskPlugin {
            id = 'net.corda.plugins.flask'
            implementationClass = 'net.corda.gradle.flask.FlaskPlugin'
        }
    }
    automatedPublishing = false
}

pluginBundle {
    website = project.vcsUrl
    vcsUrl = project.vcsUrl
    description = 'Corda Flask Plugin'
    tags = ['corda']

    plugins {
        flaskPlugin {
            displayName = 'Corda Flask Plugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    ['compileOnly', 'annotationProcessor', 'testCompileOnly', 'testAnnotationProcessor'].each { conf ->
        add(conf, [group: "org.projectlombok", name: "lombok", version: lombok_version])
    }
    testImplementation project("flask-common")
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
}

tasks.named("pluginUnderTestMetadata", PluginUnderTestMetadata) {
    pluginClasspath.from(configurations.compileClasspath)
}

tasks.withType(Test).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(11)
    }

    // Resolve warnings about "illegal reflective access" that
    // are generated by Gradle's own Groovy library.
    jvmArgs '--add-opens', 'java.base/java.io=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.util=ALL-UNNAMED',
            '--illegal-access=warn'
}

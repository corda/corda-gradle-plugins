plugins {
    id 'base'
}

description "Kotlin's metadata-handling classes"

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    proguard
    runtimeClasspath
    configurations.default.extendsFrom runtimeClasspath
}

dependencies {
    proguard "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    proguard "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    runtimeClasspath "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

def javaHome = System.getProperty('java.home')
def originalJar = configurations.proguard.files.find { it.name.startsWith("kotlin-compiler-embeddable") }

import proguard.gradle.ProGuardTask
task metadata(type: ProGuardTask) {
    injars originalJar, filter: 'META-INF/MANIFEST.MF,META-INF/metadata*.kotlin_module,**.class'
    outjars file("$buildDir/libs/${project.name}-${kotlin_version}.jar")

    libraryjars file("$javaHome/lib/rt.jar")
    libraryjars file("$javaHome/../lib/tools.jar")
    configurations.proguard.forEach {
        if (originalJar != it) {
            libraryjars it, filter: '!META-INF/versions/**'
        }
    }

    keepattributes '*'
    dontobfuscate
    dontoptimize
    verbose

    dontwarn 'org.jetbrains.annotations.**'
    dontwarn 'org.jetbrains.kotlin.com.intellij.**'
    dontwarn 'org.jetbrains.kotlin.com.google.j2objc.annotations.**'
    dontwarn 'org.jetbrains.kotlin.com.google.errorprone.annotations.**'
    dontnote

    keep 'class org.jetbrains.kotlin.load.java.JvmAnnotationNames { *; }'
    keep 'class org.jetbrains.kotlin.metadata.ProtoBuf { *; }', includedescriptorclasses: true
    keep 'class org.jetbrains.kotlin.metadata.ProtoBuf$* { *; }', includedescriptorclasses: true
    keep 'class org.jetbrains.kotlin.metadata.deserialization.** { *; }', includedescriptorclasses: true
    keep 'class org.jetbrains.kotlin.metadata.jvm.** { *; }', includedescriptorclasses: true
    keep 'class org.jetbrains.kotlin.protobuf.** { *; }', includedescriptorclasses: true
}
def metadataJar = metadata.outputs.files.singleFile

task validate(type: ProGuardTask) {
    injars metadataJar
    libraryjars file("$javaHome/lib/rt.jar")
    configurations.runtimeClasspath.forEach {
        libraryjars it, filter: '!META-INF/versions/**'
    }

    keepattributes '*'
    dontpreverify
    dontobfuscate
    dontoptimize
    verbose

    dontwarn 'org.jetbrains.kotlin.com.google.errorprone.annotations.**'
    dontnote

    keep 'class *'
}

artifacts {
    'default' file: metadataJar, name: project.name, type: 'jar', extension: 'jar', builtBy: metadata
}

defaultTasks "metadata"
metadata.finalizedBy validate

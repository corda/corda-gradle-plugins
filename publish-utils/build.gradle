plugins {
    id 'com.gradle.plugin-publish'
    id 'java-gradle-plugin'
    id 'groovy'
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    def cordaUseCache = System.getenv("CORDA_USE_CACHE")
    if (cordaUseCache != null) {
        maven {
            url = "https://software.r3.com/artifactory/corda-remotes"
            name = "R3 Maven remote repositories"
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = project.findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
                password = project.findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
            }
        }
    }
}

dependencies {
    constraints {
        implementation('commons-beanutils:commons-beanutils') {
            version {
                require commons_beanutils_version
            }
        }
        implementation('commons-codec:commons-codec') {
            version {
                require commons_codec_version
            }
        }
        implementation('commons-collections:commons-collections') {
            version {
                require commons_collections_version
            }
        }
    }

    implementation("com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version") {
        // This should be provided by Gradle itself.
        exclude group: 'org.apache.ant'
    }
}

gradlePlugin {
    plugins {
        publishUtilsPlugin {
            id = 'net.corda.plugins.publish-utils'
            implementationClass = 'net.corda.plugins.publish.PublishPlugin'
        }
    }
    automatedPublishing = false
}


pluginBundle {
    website = project.vcsUrl
    vcsUrl = project.vcsUrl
    description = 'Corda Publishing Plugin'
    tags = ['corda']

    plugins {
        publishUtilsPlugin {
            displayName = 'Corda Publishing Plugin'
        }
    }
}

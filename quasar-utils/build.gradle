plugins {
    id 'groovy'
    id 'java-gradle-plugin'
}

evaluationDependsOn("javac-plugin")

description 'A small gradle plugin for adding some basic Quasar tasks and configurations to reduce build.gradle bloat.'

repositories {
    mavenCentral()
}

gradlePlugin {
    plugins {
        quasarPlugin {
            id = 'net.corda.plugins.quasar-utils'
            implementationClass = 'net.corda.plugins.quasar.QuasarPlugin'
        }
    }
    automatedPublishing = false
}

pluginBundle {
    website = project.vcsUrl
    vcsUrl = project.vcsUrl
    description = 'Corda Quasar Utils Plugin'
    tags = ['corda']

    plugins {
        quasarPlugin {
            displayName = 'Corda Quasar Utils Plugin'
        }
    }
}

dependencies {
    implementation project(":quasar-utils:javac-plugin")
    testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version")
    testImplementation "org.assertj:assertj-core:$assertj_version"
}

FileCollection javacPluginTar = project("javac-plugin").tasks.named("tar").get().outputs.files

jar {
    manifest {
        attributes "version" : archiveVersion.get()
    }
    inputs.files(javacPluginTar)

    from(javacPluginTar) {
        into("META-INF")
        rename 'javac-plugin-.*\\.tar', 'javac-plugin.tar'
    }
}

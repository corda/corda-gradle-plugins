import java.nio.file.Files
import java.util.zip.ZipEntry
import java.util.zip.ZipFile

plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

configurations {
    quasar {
        transitive false
    }
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_jupiter_version
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junit_jupiter_version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_jupiter_version
    quasar group: 'co.paralleluniverse', name: 'quasar-core', version: '0.7.10'

    if(JavaVersion.current() <= JavaVersion.VERSION_1_8) {
        compileOnly files("${System.properties['java.home']}/../lib/tools.jar")
        testRuntimeOnly files("${System.properties['java.home']}/../lib/tools.jar")
    }
}

if(JavaVersion.current() > JavaVersion.VERSION_1_8) {
    tasks.named("compileJava", JavaCompile.class) {
        sourceCompatibility(JavaVersion.VERSION_1_8.toString())
        targetCompatibility(JavaVersion.VERSION_1_8.toString())
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'javac-plugin'
            version = project.version
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
    def testCompilationClassPath = sourceSets["main"].output.classesDirs +
            sourceSets["main"].runtimeClasspath +
            sourceSets["test"].resources.sourceDirectories + configurations.quasar
    systemProperty("test.compilation.classpath", testCompilationClassPath.asPath)
}

tasks.register("tar", Tar) {
    from(sourceSets.main.output)
}